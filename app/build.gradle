apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'org.jetbrains.kotlin.plugin.parcelize'

Properties properties = new Properties()
if (rootProject.file("project.properties").exists()) {
    properties.load(rootProject.file("project.properties").newDataInputStream())
} else {
    properties.load(rootProject.file("project-sample.properties").newDataInputStream())
}

Properties secrets = new Properties()
if (rootProject.file("secrets.properties").exists()) {
    secrets.load(rootProject.file("secrets.properties").newDataInputStream())
}

android {
    compileSdk 34
    defaultConfig {
        applicationId "edu.gvsu.art.gallery"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 1013
        versionName "2024.07.1013"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "ART_GALLERY_BASE_URL", properties['art_gallery_base_url'])
        buildConfigField("String", "APPLICATION_NAME", properties['application_name'])
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_compiler_version"
    }
    signingConfigs {
        release {
            storeFile file("${project.rootDir}/release.keystore")
            storePassword secrets['store_password']
            keyAlias secrets['key_alias']
            keyPassword secrets['store_password']
        }
        debug {
            storeFile file("${project.rootDir}/debug.keystore")
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'art-gvsu-proguard.txt'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            testCoverageEnabled true
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/services/javax.annotation.processing.Processor']
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lint {
        abortOnError false
        disable 'InvalidPackage'
    }
    namespace 'edu.gvsu.art.gallery'
}

dependencies {
    implementation platform('androidx.compose:compose-bom:2024.06.00')
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-video:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation 'androidx.media3:media3-ui:1.4.0'
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
    implementation "androidx.datastore:datastore-preferences:$datastore_version"
    implementation "androidx.compose.animation:animation"
    implementation "androidx.compose.compiler:compiler:1.5.15"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.material:material"
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-util"
    implementation "androidx.datastore:datastore-preferences:$datastore_version"
    implementation "androidx.navigation:navigation-compose:2.7.7"
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'
    implementation "com.github.Tlaster:Swiper:0.7.1"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "androidx.media3:media3-exoplayer:1.4.0"
    implementation "androidx.media3:media3-datasource-okhttp:1.4.0"
    implementation "com.squareup.sqldelight:android-driver:1.5.3"
    implementation "io.coil-kt:coil-compose:2.7.0"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation 'androidx.activity:activity-compose:1.9.1'
    implementation 'com.google.android.gms:play-services-analytics:18.1.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.gms:play-services-maps:19.0.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation platform('com.google.firebase:firebase-bom:33.1.2')
    implementation 'com.gorisse.thomas.sceneform:sceneform:1.23.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.maps.android:android-maps-utils:2.2.3'
    implementation 'com.google.maps.android:maps-ktx:3.2.1'
    implementation 'com.google.maps.android:maps-utils-ktx:3.2.1'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.android.filament:filamat-android:1.21.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':artgalleryclient')

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.mockito:mockito-core:3.6.0'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.mockito:mockito-core:3.6.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.6.8'
}

repositories {
    mavenCentral()
    google()
}

tasks.register('useGoogleServicesDebugFile') {
    description 'Copies the debug google-services.json file if file is missing.'
    doLast {
        def googleServicesFile = "google-services.json"
        if (!file("${project.projectDir}/$googleServicesFile").exists()) {
            def debugOnlyFile = "google-services-debug-only.json"
            println "$googleServicesFile file is missing. Copying $debugOnlyFile"
            copy {
                from "${project.projectDir}/$debugOnlyFile"
                into project.projectDir
                rename { googleServicesFile }
            }
        }
    }
}

preBuild.dependsOn(useGoogleServicesDebugFile)
